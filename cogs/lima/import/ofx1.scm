(require "srfi/srfi-28/format.scm")
(require "lima/config.scm")
(require "lima/types.scm")
(require "lima/list.scm")
(require "lima/alist.scm")
(require "lima/import/prelude.scm")
(require "lima/import/types.scm")
(require "lima/import/display.scm")

(define default-currency (config-value-or-default '(import default-currency) "CAD" *config*))

;; extract imported OFX1 transactions into an intermediate representation
(define (extract hdr fields txns)
  (let* ((cur (cdr-assoc-or-default "curdef" default-currency hdr))
         (dtposted-i (list-index fields "dtposted"))
         (trnamt-i (list-index fields "trnamt"))
         (fitid-i (list-index fields "fitid"))
         (name-i (list-index fields "name"))
         (memo-i (list-index fields "memo"))
         (acctid (cdr-assoc-or-default "acctid" "unknown-acctid" hdr)))
    (map (lambda (txn)
          (let* ((date (parse-date (list-ref txn dtposted-i) "%Y%m%d"))
                 (name (list-ref txn name-i))
                 (memo (list-ref txn memo-i))
                 (fitid (list-ref txn fitid-i))
                 (amt (parse-decimal (list-ref txn trnamt-i)))
                 (txnid (format "~a.~a" acctid fitid)))
            (list (cons 'date date)
              (cons 'payee name)
              (cons 'narration memo)
              (cons 'amount (amount amt cur))
              (cons 'txnid txnid))))
      txns)))

;; extract balance from header if we can find the fields we need, otherwise return empty
(define (extract-balance hdr)
  (let* ((cur (cdr-assoc-or-default "curdef" default-currency hdr))
         (balamt (cdr-assoc-or-default "balamt" '() hdr))
         (dtasof (cdr-assoc-or-default "dtasof" '() hdr)))
    (if (or (empty? balamt) (empty? dtasof))
      '()
      (let ((date (parse-date dtasof "%Y%m%d"))
            (amt (parse-decimal balamt)))
        (list (cons 'date date)
          (cons 'amount (amount amt cur)))))))

(let* ((accounts '("Assets:Bank:Current" "Expenses:Unknown"))
       (hdr (imported-header *imported*))
       (fields (imported-fields *imported*))
       (txns (imported-transactions *imported*))
       (bln (extract-balance hdr)))
  (for-each (lambda (txn) (display (format-transaction txn accounts)))
    (extract hdr fields txns))
  (unless (empty? bln)
    (display-balance bln (car accounts))))
