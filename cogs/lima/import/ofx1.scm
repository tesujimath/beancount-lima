(require "srfi/srfi-28/format.scm")
(require "lima/config.scm")
(require "lima/types.scm")
(require "lima/list.scm")
(require "lima/alist.scm")
(require "lima/stdlib.scm")
(require "lima/import/prelude.scm")
(require "lima/import/extract.scm")
(require "lima/import/types.scm")
(require "lima/import/display.scm")
(require "lima/import/account-inference.scm")
(require "lima/import/dedupe.scm")

(define default-currency (config-value-or-default '(import default-currency) "CAD" *config*))

;
;; extract imported OFX1 transactions into an intermediate representation
(define (make-extract field-names acctid primary-account cur)
  (let* ((get-dtposted (make-field-getter field-names "dtposted" (lambda (x) (parse-date x "%Y%m%d"))))
         (get-trnamt (make-field-getter field-names "trnamt" parse-decimal))
         (get-fitid (make-field-getter field-names "fitid" identity))
         (get-name (make-field-getter field-names "name" identity))
         (get-memo (make-field-getter field-names "memo" identity)))
    (lambda (txn)
      (list (cons 'date (get-dtposted txn))
        (cons 'payee (get-name txn))
        (cons 'narration (get-memo txn))
        (cons 'amount (amount (get-trnamt txn) cur))
        (cons 'primary-account primary-account)
        (cons 'txnid (format "~a.~a" acctid (get-fitid txn)))))))

;; extract balance from header if we can find the fields we need, otherwise return empty
(define (extract-balance hdr)
  (let* ((cur (cdr-assoc-or-default 'curdef default-currency hdr))
         (balamt (cdr-assoc-or-empty 'balamt hdr))
         (dtasof (cdr-assoc-or-empty 'dtasof hdr)))
    (if (or (empty? balamt) (empty? dtasof))
      '()
      ;; Beancount balance date is as of midnight at the beginning of the day, but we have the end of the day, so add 1 day
      (let ((date (date-after (parse-date dtasof "%Y%m%d") 1))
            (amt (parse-decimal balamt)))
        (list (cons 'date date)
          (cons 'amount (amount amt cur)))))))

(let* ((accounts-by-id (config-value-or-default '(import accounts) '() *config*))
       (txnid-key (config-value-or-default '(import txnid-key) "txnid" *config*))
       (hdr (imported-header *imported*))
       (cur (cdr-assoc-or-default 'curdef default-currency hdr))
       (acctid (cdr-assoc-or-default 'acctid "unknown-acctid" hdr))
       (primary-account (cdr-assoc-or-default acctid "Assets:Unknown" accounts-by-id))
       (field-names (imported-fields *imported*))
       (txns (imported-transactions *imported*))
       (payees (imported-payees *imported*))
       (narrations (imported-narrations *imported*))
       (existing-txnids (imported-txnids *imported*))
       (bln (extract-balance hdr))
       (txn-directive (config-value-or-default '(import txn-directive) "txn" *config*)))
  (transduce txns
    (mapping (make-extract field-names acctid primary-account cur))
    (filtering (make-dedupe-transactions existing-txnids))
    (mapping (make-infer-secondary-accounts-from-payees-and-narrations payees narrations))
    (into-for-each (lambda (txn) (display (format-transaction txn txn-directive #:txnid-key txnid-key)))))
  (unless (empty? bln)
    (display (format-balance bln primary-account))))
